CREATE PROCEDURE [Retail_Sales_Wrk].[usp_SUOrder_LoadQueue_GetOrderID]
(
    @PID UNIQUEIDENTIFIER,
    @SUOrderID VARCHAR(50) OUTPUT
)
AS

BEGIN

	DECLARE
			@String VARCHAR(5000),
			@DateValue DATETIME,
			@User VARCHAR(500),
			@DestinationDatabase VARCHAR(150),
			@DestinationSchema VARCHAR(150),
			@DestinationTable VARCHAR(150);
			      
	SET @String = 'Retail_Sales_Wrk.usp_SUOrder_LoadQueue_GetOrderID';
	SET @User = SYSTEM_USER;
	SET @DateValue = GETDATE();

	SELECT
		@DateValue = CSTDateValue
	FROM [$(ETL_Framework)].[DW_Developer].fn_GetDate(@DateValue);

	INSERT INTO [$(ETL_Framework)].[DW_Developer].[AuditLog]
	VALUES
	(
		@String, @DateValue, @User, 'Process Start'
	);

	BEGIN TRY

		SET @SUOrderID = NULL;
    
		-- Get the first available OrderID
		SELECT TOP 1 @SUOrderID = SUOrderID
		FROM [Retail_Sales_Wrk].[SUOrderLoadQueue]
		WHERE ProcessStatus = 0
		ORDER BY SUOrderID;
    
		-- Update the record if we found one
		IF @SUOrderID IS NOT NULL
		BEGIN

			UPDATE [Retail_Sales_Wrk].[SUOrderLoadQueue]
			SET PID = @PID,
				ProcessStatus = 1
			WHERE SUOrderID = @SUOrderID
			AND ProcessStatus = 0;

		END

		SET @DateValue = GETDATE();

		SELECT
			@DateValue = CSTDateValue
		FROM [$(ETL_Framework)].[DW_Developer].fn_GetDate(@DateValue);

		INSERT INTO [$(ETL_Framework)].[DW_Developer].[AuditLog]
		VALUES
		(
			@String, @DateValue, @User, 'Process Complete'
		);
	
	END TRY

	BEGIN CATCH
        
		DECLARE
			@ErrorMessage  VARCHAR(4000),
			@ErrorSeverity INT,
			@ErrorState    INT;

		SET @ErrorMessage = ERROR_MESSAGE();
		SET @ErrorSeverity = ISNULL(ERROR_SEVERITY(), 16);
		SET @ErrorState = ISNULL(ERROR_STATE(), 0);
		SET @DateValue = GETDATE();

		SELECT
			@DateValue = CSTDateValue
		FROM [$(ETL_Framework)].[DW_Developer].fn_GetDate(@DateValue);

		INSERT INTO [$(ETL_Framework)].[DW_Developer].[AuditLog]
		VALUES
		(
			@String, @DateValue, @User, @ErrorMessage
		);

		RAISERROR(@ErrorMessage, @ErrorSeverity, @ErrorState);

	END CATCH

END